buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.owasp:dependency-check-gradle:6.5.1'
    }
}

plugins {
    id 'java'
}

apply plugin: 'org.owasp.dependencycheck'

group 'com.zacharytalis'
version '1.0'

sourceCompatibility = 17

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.javacord:javacord:3.3.2'

    implementation 'org.apache.logging.log4j:log4j-core:2.17.0'
    implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.17.0'
    implementation 'ch.qos.logback:logback-classic:1.2.10'

    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'javax.mail:mail:1.4.7'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

task cleanSetupFiles(type: Delete) {
    delete "${buildDir}/libs/lib"
}

task copyToLib(type: Copy) {
    into "${buildDir}/libs/lib"
    from configurations.runtimeClasspath
}

task copySetupFiles(type: Copy) {
    into "${buildDir}/libs"
    from ("${rootDir}/entrypoint.sh")
}

task zipBuild(type: Zip) {
    from "${buildDir}/libs/"
    include '*'
    include '*/*'
    archiveName 'alt-text-bot.zip'
    destinationDir(file("${buildDir}/distributions/"))
}

tasks.copyToLib.shouldRunAfter(tasks.cleanSetupFiles)
tasks.copySetupFiles.shouldRunAfter(tasks.copyToLib)
tasks.zipBuild.shouldRunAfter(tasks.copySetupFiles)

jar {
    dependsOn cleanSetupFiles
    dependsOn copyToLib
    dependsOn copySetupFiles
    dependsOn zipBuild

    manifest {
        attributes  "Main-Class": "com.zacharytalis.alttextbot.EntryPoint",
                    "Class-Path": configurations.runtimeClasspath.collect {"lib/$it.name" }.join(' ')
    }

    archiveBaseName.set('AltTextBot')

    exclude "META-INF/*.MF"
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
}

tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}

check.dependsOn dependencyCheckAnalyze
